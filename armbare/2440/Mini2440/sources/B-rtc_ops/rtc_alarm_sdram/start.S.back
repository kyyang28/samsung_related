
.equ    rWTCON,    0x53000000

.text
.global _start
_start:
	/* 0x0 */
    b    reset

    /* 0x4 */
    b    _undefined_instruction	

    /* 0x8 */
    b    _swi

    /* 0xC */
    b    _prefetch_abort 

    /* 0x10 */
    b    _data_abort 

    /* 0x14 */
    b    _reserved 

    /* 0x18 */
    b    _irq  

    /* 0x1C */
    b    _fiq  


reset:
    /* Disable watchdog */
    mov r0, #0
    ldr r1, =rWTCON
    str r0, [r1]

    bl clock_asm_init
    bl sdram_asm_init
    
	ldr sp, =4096
    
	bl nand_init_ll

    adr r0, _start
	ldr	r1, =_start
	ldr r2, =__bss_start
    sub r2, r2, r1
    bl  copy2sdram

    bl  clear_bss

    msr cpsr_c, #0xD2 // IRQ mode
    ldr sp, =0x31000000

    msr cpsr_c, #0xDF
    ldr sp, =0x34000000

    ldr lr, =rtc_irq_init_return
    ldr pc, =rtc_irq_init

rtc_irq_init_return:

    ldr lr, =infinite_loop
    ldr pc, =rtc_main

infinite_loop:
    b   infinite_loop


_undefined_instruction:
    b    _undefined_instruction

_swi:
    b    _swi

_prefetch_abort:
    b    _prefetch_abort

_data_abort:
    b    _data_abort

_reserved:
    b    _reserved

_irq:
    sub   lr, lr, #4
    stmdb sp!, {r0-r12, lr}

    ldr   lr, =_int_return
    ldr   pc, =rtc_int_handler

_int_return:
    ldmia sp!, {r0-r12, pc}^

_fiq:
    b    _fiq


